apply plugin: 'checkstyle'
apply plugin: 'findbugs'
apply plugin: 'pmd'
apply plugin: 'jacoco'

checkstyle {
    ignoreFailures = true
    configFile = rootProject.file('codequality/checkstyle.xml')
    toolVersion = "8.8"
}

findbugs {
    ignoreFailures = false
    sourceSets = [ project.sourceSets.main ]
}

pmd {
    sourceSets = [ project.sourceSets.main ]
}

jacoco {

    toolVersion = '0.8.0'

}

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled true
        html.destination file("$buildDir/reports/jacoco")
    }

    afterEvaluate {
        classDirectories = files(classDirectories.files.collect {
            fileTree(dir: it,
                    exclude: [
                            // inherited from blueprint
                            '**/server/**',
                            '**/phylon/**',
                            '**/Application.*',
                            '**/configuration/**'
                    ])
        })
    }
}

test {
    jacoco {
        append = false
        destinationFile = file("$buildDir/jacoco/jacocoTest.exec")
        classDumpDir = file("$buildDir/jacoco/classpathdumps")
    }
    finalizedBy jacocoTestReport
}

tasks.withType(FindBugs) {
    reports {
        xml.withMessages = true
    }
}

task checkstyleHtml {
    group = "Verification"
    description = "Creates a human readable Checkstyle report"
    doLast {
        ant.xslt(in: checkstyleMain.reports.xml.destination,
                style: file('codequality/checkstyle-noframes-sorted.xsl'),
                out: new File(checkstyleMain.reports.xml.destination.parent, 'main.html'))
    }
}
checkstyleMain.finalizedBy checkstyleHtml

task findbugsHtml {
    group = "Verification"
    description = "Creates a human readable Findbugs report"
    doLast {
        ant.xslt(in: file("$buildDir/reports/findbugs/main.xml"),
                style: file('codequality/findbugs_default.xsl'),
                out: new File("$buildDir/reports/findbugs/main.html"))
    }
}
findbugsMain.finalizedBy findbugsHtml
